plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.10'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
    id 'org.asciidoctor.jvm.convert' version "3.3.2"
}

group = 'com.project'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    asciidoctorExtensions
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    set('snippetsDir', file("build/generated-snippets"))
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    // (1) asciidoctor: build/generated-snippets에 생성된 adoc 조각들을 프로젝트 내 .adoc 파일에서 읽어들이도록 연동한다.
    asciidoctorExtensions 'org.springframework.restdocs:spring-restdocs-asciidoctor'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'com.mysql:mysql-connector-j'

    testImplementation 'io.rest-assured:rest-assured:4.4.0'
    testImplementation 'io.rest-assured:spring-mock-mvc:4.4.0'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
}

test {
    outputs.dir snippetsDir
    useJUnitPlatform()
}

asciidoctor {
    // 위에서 작성한 설정 적용
    configurations 'asciidoctorExtensions'

    // 특정 .adoc 파일만 html로 생성(sources가 없다면 모든 파일을 html로 만든다.)
    sources {
        include("**/index.adoc", "**/common/*.adoc")
    }

    // 특정 .adoc에 다른 adoc 파일을 가져와서(include), 사용하고 싶을 경우 경로를 baseDir로 맞춰준다.
    // (개별 adoc으로 운영한다면 필요 없는 옵션)
    baseDirFollowsSourceDir()

    // snippetDir를 입력으로 구성
    inputs.dir snippetsDir

    // asciidoctor 전 test 실행
    dependsOn test
}

// 테스트를 실행하게되면 기존 static/docs/ 비우기
asciidoctor.doFirst {
    delete file('src/man/resources/static/docs')
}

// asciidoctor 작업 이후 생성된 HTML파일을 static/docs/로 카피
task createDocument(type: Copy) {
    dependsOn asciidoctor

    from file("build/docs/asciidoc")
    into file("src/main/resources/static")
}

// build 전 createDocument(REST Docs 문서화) 실행
build {
    dependsOn createDocument
}

